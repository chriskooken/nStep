%header%

GRAMMARTYPE	= "LL"

DESCRIPTION	= "Grammar for Gherkin Language
			   Limitations: No suppport for tags.
			                Feature, Background, Scenario, and Examples must be flush to the left
"

%tokens%

T_FEATURE			= <<Feature:?>>
T_BACKGROUND		= <<Background:?>>
T_SCENARIO			= <<Scenario:?>>
T_SCENARIO_OUTLINE	= <<Scenario Outline:?>>
T_EXAMPLES			= <<Examples:?>>

T_GIVEN	= <<Given|given:>>
T_WHEN	= <<When|when:>>
T_THEN	= <<Then|then:>>
T_AND	= <<And|and:>>
T_BUT	= <<But|but:>>

HORIZONTAL_WHITESPACE	= <<[\t ]>>
EOL						= <<\r?\n>>					
COMMENT					= <<[\t ]*#[^\r^\n]*\r?\n>>	%ignore%

TEXT_CHAR				= <<[^\r^\n^\|^\t^ ]>>

PIPE					= "|"

%productions%

Feature					= FeatureHeader BlankLine* Background? (Scenario | ScenarioOutline)* ;
FeatureHeader			= T_FEATURE SummaryLine+ ;
SummaryLine				= FreeLine ;

Background				= BackgroundHeader Step+ ;
BackgroundHeader		= T_BACKGROUND FreeLine ;

Scenario				= ScenarioHeader Step+ ;
ScenarioHeader			= T_SCENARIO FreeLine ;

ScenarioOutline			= ScenarioOutlineHeader Step+ Examples BlankLine* ;
ScenarioOutlineHeader	= T_SCENARIO_OUTLINE FreeLine ;

Examples				= ExamplesHeader Table ;
ExamplesHeader			= T_EXAMPLES EOL ;

Step					= Whitespace? (Given | When | Then | And | But) | BlankLine ;

Given					= T_GIVEN FreeLine ;
When					= T_WHEN FreeLine ;
Then					= T_THEN FreeLine ;
And						= T_AND FreeLine ;
But						= T_BUT FreeLine ;

Table					= TableRow+ ;
TableRow				= "|" (TableColumn "|")+ EOL ;
TableColumn				= Text ;

Whitespace				= HORIZONTAL_WHITESPACE+ ;
FreeLine				= Text? EOL ;
BlankLine				= EOL ;
Text					= (HORIZONTAL_WHITESPACE | TEXT_CHAR)+ ;

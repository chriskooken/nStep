%header%

GRAMMARTYPE	= "LL"

DESCRIPTION	= "Grammar for Gherkin Language
			   Limitations: No suppport for tags.
			                Feature, Background, Scenario, and Examples must be flush to the left
			                Blank lines must be completely blank (no whitespace)
"

%tokens%

T_FEATURE			= <<Feature:?>>
T_BACKGROUND		= <<Background:?>>
T_SCENARIO			= <<Scenario:?>>
T_SCENARIO_OUTLINE	= <<Scenario Outline:?>>
T_EXAMPLES			= <<Examples:?>>

T_GIVEN	= <<Given|given:>>
T_WHEN	= <<When|when:>>
T_THEN	= <<Then|then:>>
T_AND	= <<And|and:>>
T_BUT	= <<But|but:>>

HORIZONTAL_WHITESPACE	= <<[\t ]>>
EOL						= <<\r?\n>>					
COMMENT					= <<[\t ]*#[^\r^\n]*\r?\n>>	%ignore%

TEXT_CHAR				= <<[^\r^\n^\|^\t^ ^@^:]>>
PIPE					= "|"
COLON					= ":"
AT						= "@"

%productions%

Feature					= Tags? FeatureHeader BlankLine* Background? FeatureItem* ;
FeatureHeader			= T_FEATURE SummaryLine+ ;
SummaryLine				= FreeLine ;

Background				= BackgroundHeader Step+ ;
BackgroundHeader		= T_BACKGROUND FreeLine ;

FeatureItem				= Tags? (Scenario | ScenarioOutline) ;

Scenario				= ScenarioHeader Step+ ;
ScenarioHeader			= T_SCENARIO FreeLine ;

ScenarioOutline			= ScenarioOutlineHeader Step+ Examples BlankLine* ;
ScenarioOutlineHeader	= T_SCENARIO_OUTLINE FreeLine ;

Examples				= ExamplesHeader Table ;
ExamplesHeader			= T_EXAMPLES FreeLine ;

Step					= Whitespace? (T_GIVEN | T_WHEN | T_THEN | T_AND | T_BUT) Text (EOL | COLON EOL Table) | BlankLine ;

Table					= TableRow+ ;
TableRow				= Whitespace? "|" (TableColumn "|")+ EOL ;
TableColumn				= Text ;

Whitespace				= HORIZONTAL_WHITESPACE+ ;
FreeLine				= Text? EOL ;
BlankLine				= EOL ;
Text					= Whitespace? (TEXT_CHAR | AT | T_FEATURE | T_GIVEN | T_WHEN | T_THEN | T_AND | T_BUT) (HORIZONTAL_WHITESPACE | TEXT_CHAR | AT | T_FEATURE | T_BACKGROUND | T_SCENARIO | T_SCENARIO_OUTLINE | T_EXAMPLES | T_GIVEN | T_WHEN | T_THEN | T_AND | T_BUT)+ ;
Tags					= Tag (Whitespace Tag)* EOL ;
Tag						= "@" TEXT_CHAR+ ;
